apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'war'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
}

String springVersion = '4.0.2.RELEASE'
String springSecurityVersion = '3.2.0.RELEASE'
String hibernateVersion = '4.2.7.Final'
String slf4jVersion = '1.7.5'

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.springframework') {
            details.useVersion springVersion
        }
    }
}

dependencies {
    providedCompile 'javax.servlet:javax.servlet-api:3.0.1'

    //core spring
    compile ('org.springframework:spring-context:' + springVersion)
    compile ('org.springframework:spring-aop:' + springVersion)
    compile ('org.springframework:spring-beans:' + springVersion)
    compile ('org.springframework:spring-core:' + springVersion)
    compile ('org.springframework:spring-expression:' + springVersion)
    compile ('org.springframework:spring-webmvc:' + springVersion)
    compile ('org.springframework.security:spring-security-config:' + springSecurityVersion) //TODO: exclude spring
    compile ('org.springframework.security:spring-security-web:' + springSecurityVersion) //TODO: exclude spring

    //aspects
    compile 'org.aspectj:aspectjrt:1.7.3'
    compile 'org.aspectj:aspectjweaver:1.7.3'


    //logging
    compile ('org.slf4j:jcl-over-slf4j:' + slf4jVersion)
    compile ('org.slf4j:jul-to-slf4j:' + slf4jVersion)
    compile ('ch.qos.logback:logback-classic:1.0.13')

    //json
    compile 'com.fasterxml.jackson.core:jackson-databind:2.2.3'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
    compile 'org.codehaus.jackson:jackson-core-asl:1.9.13'

    //db access
    compile 'org.hibernate:hibernate-validator:4.3.1.Final'
    compile 'org.springframework:spring-jdbc:' + springVersion
    compile 'org.springframework:spring-orm:' + springVersion
    compile 'org.springframework.data:spring-data-jpa:1.4.3.RELEASE' //TODO: exclude everything
    compile 'org.apache.tomcat:tomcat-jdbc:7.0.42'
    compile 'org.hibernate:hibernate-entitymanager:' + hibernateVersion
    compile 'org.hibernate:hibernate-core:' + hibernateVersion
    compile 'org.hibernate:hibernate-ehcache:4.2.7.Final'
    compile 'org.hsqldb:hsqldb:2.3.1'

    //other
    compile 'com.google.guava:guava:16.0'
    compile 'joda-time:joda-time:2.3'
    compile 'org.jadira.usertype:usertype.jodatime:2.0.1'
    compile 'org.codehaus.groovy:groovy-all:2.2.1'

    //testing
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile "org.mockito:mockito-all:1.9.0"
    testCompile "org.hamcrest:hamcrest-core:1.3" // only necessary if Hamcrest matchers are used
    testCompile("org.spockframework:spock-core:0.7-groovy-2.0") {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testRuntime("org.spockframework:spock-spring:0.7-groovy-2.0") {
        exclude group: 'org.spockframework', module: 'spock-core'
    }
    testRuntime "cglib:cglib-nodep:2.2.2" // allows mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:1.2" // allows mocking of classes without default constructor (together with CGLIB)
    testCompile 'org.springframework:spring-test:' + springVersion
    testRuntime 'com.jayway.jsonpath:json-path:0.8.1'
}

sourceSets {
    test {
        groovy {
            srcDirs = ['src/test/groovy']
        }
    }
}

war {
    baseName = 'sample spring4 setup'
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}